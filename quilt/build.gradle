plugins {
    id 'org.quiltmc.loom' version '1.4+'
}

version = project.mod_version + "-" + project.minecraft_version + "-quilt"
group = project.mod_group_id

base {
    archivesBaseName = project.mod_id
}

loom {
    mods {
        "${project.mod_id}" {
            sourceSet("main")
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
    }
    modImplementation "org.quiltmc:quilt-loader:${project.quilt_loader_version}"
    modImplementation "org.quiltmc:qsl:${project.qsl_version}+${project.minecraft_version}"
}

sourceSets {
    main {
        java {
            srcDir "../src/main/java"
        }
        resources {
            srcDir "../src/main/resources"
        }
    }
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
        minecraft_version_range : project.quilt_minecraft_version_range,
        quilt_loader_version_range : project.quilt_loader_version_range,
        version: project.mod_version,
        pack_format_number: project.pack_format_number,
    ]
    inputs.properties replaceProperties
    filesMatching(['quilt.mod.json', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

    withSourcesJar()
}

jar {
    from("../LICENSE") {
    }
}
